/*
 * Copyright 2019 Haulmont.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    ext {
        jmixPluginVersion = '1.0-SNAPSHOT'
    }
    repositories {
        if (rootProject.hasProperty('jmixRepoUrl')) {
            maven {
                url rootProject['jmixRepoUrl']

                if (rootProject.hasProperty('jmixRepoUser') && rootProject.hasProperty('jmixRepoPassword')) {
                    credentials {
                        username rootProject['jmixRepoUser']
                        password rootProject['jmixRepoPassword']
                    }
                }
            }
        } else {
            jcenter()
            maven {
                url "https://plugins.gradle.org/m2/"
            }
        }
    }
    dependencies {
        classpath("io.jmix.gradle:jmix-gradle-plugin:${jmixPluginVersion}")
        classpath("gradle.plugin.com.github.spotbugs.snom:spotbugs-gradle-plugin:4.0.4")
    }
}

allprojects {
    repositories {
        if (rootProject.hasProperty('jmixRepoUrl')) {
            maven {
                url rootProject['jmixRepoUrl']

                if (rootProject.hasProperty('jmixRepoUser') && rootProject.hasProperty('jmixRepoPassword')) {
                    credentials {
                        username rootProject['jmixRepoUser']
                        password rootProject['jmixRepoPassword']
                    }
                }
            }
        } else {
            jcenter()
            maven { url "https://nexus.jmix.io/repository/public" }
        }
    }
}

subprojects {
    ext.jmixFrameworkItself = true

    plugins.withType(JavaPlugin) {
        java {
            sourceCompatibility = JavaVersion.VERSION_1_8
            targetCompatibility = JavaVersion.VERSION_1_8
        }
    }

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    tasks.withType(Test) {
        systemProperty('org.slf4j.simpleLogger.defaultLogLevel', 'debug')
        systemProperty('org.slf4j.simpleLogger.log.org.springframework', 'info')
        systemProperty('org.slf4j.simpleLogger.log.eclipselink.sql', 'debug')
    }

    tasks.withType(Javadoc) {
        options.addStringOption("sourcepath", "")
        options.encoding = 'UTF-8'
        options.memberLevel = org.gradle.external.javadoc.JavadocMemberLevel.PROTECTED
        destinationDir = file("$buildDir/docs/javadoc")

        title = "${project.name.toUpperCase()} ${project.version.replace('-SNAPSHOT','')} API"
    }
}

configure(subprojects.findAll { !it.name.startsWith('sample-') }) {
    apply plugin: 'maven-publish'
    apply plugin: 'maven' // remove after https://youtrack.jetbrains.com/issue/IDEA-227215 is fixed

    publishing {
        if (rootProject.hasProperty('jmixUploadUrl')) {
            repositories {
                maven {
                    url = version.endsWith('SNAPSHOT') ? "$jmixUploadUrl/snapshots" : "$jmixUploadUrl/releases"
                    credentials {
                        username rootProject['jmixUploadUser']
                        password rootProject['jmixUploadPassword']
                    }
                }
            }
        }
    }
}

configure([project(':modules:core'), project(':modules:data'), project(':modules:security'), project(':modules:audit'),
        project(':modules:ui')]) {
    if (rootProject.hasProperty('spotbugsEnabled')) {
        apply plugin: 'com.github.spotbugs'

        project.afterEvaluate {
            spotbugs {
                toolVersion = '4.0.1'
                ignoreFailures = false
                omitVisitors = ['FindDoubleCheck']
                excludeFilter = rootProject.file("etc/spotbugs/spotbugs-exclude-filter.xml")
                effort = "max"
                reportLevel = "medium"
            }

            spotbugsMain {
                jvmArgs = ['-Xmx1024m']

                reports {
                    xml.enabled = false
                    html {
                        enabled = true
                        stylesheet = rootProject.resources.text.fromFile('etc/spotbugs/spotbugs-fancy-hist.xsl')
                        destination file("${project.buildDir}/reports/spotbugs/${project.name}.html")
                    }
                }
            }

            spotbugsTest {
                jvmArgs = ['-Xmx1024m']

                reports {
                    xml.enabled = false
                    html {
                        enabled = true
                        stylesheet = project.resources.text.fromFile('etc/spotbugs/spotbugs-fancy-hist.xsl')
                        destination file("${project.buildDir}/reports/spotbugs/test-${project.name}.html")
                    }
                }
            }
        }
    }
}
