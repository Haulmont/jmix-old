/*
 * Copyright 2019 Haulmont.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

ext {
    jmixVersion = '0.0.1-SNAPSHOT'
}

repositories {
    if (rootProject.hasProperty('jmixRepoUrl')) {
        maven {
            url rootProject['jmixRepoUrl']

            if (rootProject.hasProperty('jmixRepoUser') && rootProject.hasProperty('jmixRepoPassword')) {
                credentials {
                    username rootProject['jmixRepoUser']
                    password rootProject['jmixRepoPassword']
                }
            }
        }
    } else {
        jcenter()
        maven { url "https://dl.bintray.com/cuba-platform/main" }
    }
}

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'maven-publish'

dependencies {
    implementation gradleApi()
    implementation localGroovy()
    
    implementation('org.javassist:javassist:3.24.1-GA')
    implementation('commons-io:commons-io:2.6')
    implementation('org.apache.commons:commons-lang3:3.9')
    implementation('com.google.guava:guava:27.1-jre')

    implementation('com.yahoo.platform.yui:yuicompressor:2.4.6')
    implementation('com.vaadin:vaadin-sass-compiler:0.9.13') {
        exclude(group: 'com.yahoo.platform.yui', module: 'yuicompressor')
    }
}

group = 'io.jmix.gradle'
version = jmixVersion

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

publishing {
    if (rootProject.hasProperty('jmixUploadUrl')) {
        repositories {
            maven {
                url = jmixVersion.endsWith('SNAPSHOT') ? "$jmixUploadUrl/snapshots" : "$jmixUploadUrl/releases"
                credentials {
                    username rootProject['jmixUploadUser']
                    password rootProject['jmixUploadPassword']
                }
            }
        }
    }

    publications {
        javaMaven(MavenPublication) {
            groupId = 'io.jmix.gradle'
            artifactId = 'jmix-gradle-plugin'

            from components.java
            artifact sourcesJar
        }
    }
}
